# Maintainer: Tomasz Maciej Nowak <com[dot]gmail[at]tmn505>
# patch idea taken from package brother-hl-l2380dw by apaugh

# All my PKGBUILDs are managed at https://github.com/tmn505/AUR

pkgname=brother-dcp-l2500d
pkgver=3.2.0_1
pkgrel=5
pkgdesc='LPR driver and CUPS wrapper for the Brother DCP-L2500D'
arch=('i686' 'x86_64')
url='http://solutions.brother.com/linux'
license=('GPL-2.0-or-later' 'custom: Brother EULA')
depends=('cups' 'perl')
depends_x86_64=('lib32-glibc')
optdepends=('brscan4: scanner support.')
source=("http://download.brother.com/welcome/dlf101753/dcpl2500dlpr-${pkgver/_/-}.i386.rpm"
	"http://download.brother.com/welcome/dlf101754/dcpl2500dcupswrapper-${pkgver/_/-}.i386.rpm"
	"${pkgname}-basedir.patch"
	"${pkgname}-eula.txt")
sha256sums=('ec739bb742cb16f943e5faeed845b317ff6608229b4826c8738e5076f90e810e'
            'd53d13a0854f8a8fd50bdbaf3fe2edead7c52d17f11bb86c64d000d5ffd28aef'
            '1c675b0318bd4b17a5860df618c293265a0d15ca496b8afe02002c723c5112b2'
            'cc213d39be5deb288016ea49fcd17ee352baf514177b299b348724d2cd5a14fc')

prepare() {
	patch -p1 -i ${srcdir}/${pkgname}-basedir.patch
}

package() {
	cp -R ${srcdir}/opt ${pkgdir}/
	rm ${pkgdir}/opt/brother/Printers/DCPL2500D/cupswrapper/Copying
	install -Dm644 ${srcdir}/${pkgname}-eula.txt ${pkgdir}/usr/share/licenses/${pkgname}/eula.txt
	ln -s /usr/share/licenses/GPL2/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-license.txt
	install -dm755 ${pkgdir}/usr/share/cups/model
	ln -s /opt/brother/Printers/DCPL2500D/cupswrapper/brother-DCPL2500D-cups-en.ppd \
		${pkgdir}/usr/share/cups/model/brother-DCPL2500D-cups-en.ppd
	install -dm755 ${pkgdir}/usr/lib/cups/filter
	ln -s /opt/brother/Printers/DCPL2500D/cupswrapper/brother_lpdwrapper_DCPL2500D \
		${pkgdir}/usr/lib/cups/filter/brother_lpdwrapper_DCPL2500D
}
